plugins {
    id 'java'
    id 'idea' // 추가
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }
}

// root 하위의 모든 모듈에 적용할 설정
subprojects {

    // plugin 블럭 사용 불가
    // apply plugin 사용
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }
    // 공통 dependency
    dependencies {
        // lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0' // 버전 명시 안해서 컴파일 오류

        // mapstruct
        implementation 'org.mapstruct:mapstruct:1.5.3.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

        // swagger
        implementation 'org.springdoc:springdoc-openapi-ui:1.6.13'

        // jackson
        // 이미 spring-boot-starter-web에 들어가있는 종속성
//        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.4'


        // test 관련
        // mapstruct
//        testImplementation 'org.mapstruct:mapstruct:$1.5.3.Final'
        testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    test {
        useJUnitPlatform()
    }
}

project(':model') {
    // 공통 코드
    bootJar { enabled = false } // core 은 bootJar 로 패키징 할 필요 없음
    jar { enabled = true }

    dependencies {
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.mysql:mysql-connector-j' // runtimeOnly는 api 옵션 없음. 그래도, 다른 모듈에서 dependency로 가지고 있음


    }
}

project(':admin') {
    bootJar { enabled = true } // main이 필요한 모듈에 대해서만 bootJar 사용
    jar { enabled = false }

    dependencies {
        implementation project(':model')
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}

project(':service-api') {
    bootJar { enabled = true } // main이 필요한 모듈에 대해서만 bootJar 사용
    jar { enabled = false }

    dependencies {
        implementation project(':model')
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}



